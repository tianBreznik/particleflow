<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Particle Flow</title>
    <link rel="icon" type="image/jpg" href="https://github.com/tianBreznik/particleflow/blob/master/src/murimacek.jpg"/>
    <style>
        body { margin: 0; }
        canvas { display: block; }

        #info {
            position: absolute;
            top: 15px;
            left: 15px;
            /* Fallback for web browsers that don't support RGBa */
            background-color: rgb(0, 0, 0);
            /* RGBa with 0.6 opacity */
            background-color: rgba(255, 255, 255, 0.85);
            /* For IE 5.5 - 7*/
            filter:progid:DXImageTransform.Microsoft.gradient(startColorstr=#99000000, endColorstr=#99000000);
            /* For IE 8*/
            -ms-filter: "progid:DXImageTransform.Microsoft.gradient(startColorstr=#99000000, endColorstr=#99000000)";
            border: 1px solid #000000;
            width: 200px;
            padding: 10px;
            color: #000000;
            text-align: justify;
            font-size: 12px;
            /* font-family: Gigi; */
            font-family: Bahnschrift;
        }

        #nametag {
            font-family: Gigi;
        }

    </style>
</head>
<body>
<script type="module" src="dist/bundle.js"></script>
<script type="glsl" id="thomas_attractor_shader">
uniform float timestep;
uniform float normalizeFactor;

// Thomas Attractor parameters
float a = 0.15;

void main() {

    vec2 uv = gl_FragCoord.xy / resolution.xy;

    vec3 pos = texture2D(texturePosition, uv).xyz;

    // Previous positions
    float x = pos.x;
    float y = pos.y;
    float z = pos.z;

    float dx = - a * x + sin(y);
    float dy = - a * y + sin(z);
    float dz = - a * z + sin(x);

    vec3 velocity = vec3(dx, dy, dz) * timestep;
    velocity = mix(velocity, normalize(velocity), normalizeFactor);

    gl_FragColor = vec4(velocity, 1.0);
}
</script>
<script type="glsl" id="halvorsen_attractor_shader">
// adapted from https://www.dynamicmath.xyz/strange-attractors/

uniform float timestep;
uniform float normalizeFactor;

// halvorsen Attractor parameters
float a = 1.89;


void main() {

    vec2 uv = gl_FragCoord.xy / resolution.xy;

    vec3 position = texture2D(texturePosition, uv).xyz;

    // Previous positions
    float x = position.x;
    float y = position.y;
    float z = position.z;

    float dx = -a*x - 4.0*y - 4.0*z - y*y;
    float dy = -a*y - 4.0*z - 4.0*x - z*z;
    float dz = -a*z - 4.0*x - 4.0*y - x*x;

    vec3 velocity = vec3(dx, dy, dz) * timestep;
    velocity = mix(velocity, normalize(velocity), normalizeFactor);

    gl_FragColor = vec4(velocity, 1.0);
}
</script>
<script type="glsl" id="chen_attractor_shader">
//addapted from http://www.3d-meier.de/tut5a/Seite42.html
uniform float normalizeFactor;
uniform float timestep;

// Chen Attractor parameters
float a = 5.0;
float b = -10.0;
float c = -0.38;
//float d = -1.0;

void main() {

    vec2 uv = gl_FragCoord.xy / resolution.xy;

    vec3 position = texture2D(texturePosition, uv).xyz;

    // Previous positions
    float x = position.x;
    float y = position.y;
    float z = position.z;

    float dx = a*x - y*z;
    float dy = b*y + x*z;
    float dz = c*z + (x*y)/3.0;

    vec3  velocity = vec3(dx, dy, dz) * timestep * 0.1;
    velocity = mix(velocity, normalize(velocity), normalizeFactor);

    gl_FragColor = vec4(velocity, 1.0);
}
</script>
<script type="glsl" id="lu_chen_attractor_shader">
//addapted from http://www.3d-meier.de/tut5a/Seite42.html
uniform float normalizeFactor;
uniform float timestep;

// Chen Attractor parameters
float a = 40.0;
float b = 3.0;
float c = 28.0;
float d = 5.51;
float e = 0.0;

float g_of_x(float x_i){
    return e * x_i + (d+e)*(abs(x_i+1.0)-abs(x_i-1.0));
}
void main() {

    vec2 uv = gl_FragCoord.xy / resolution.xy;

    vec3 position = texture2D(texturePosition, uv).xyz;

    // Previous positions
    float x = position.x;
    float y = position.y;
    float z = position.z;

    float dx = (a*(y-x))/12.5;
    float dy = ((c-a)*x - x*z + c*y)/12.5;
    float dz = (x*y-b*z)/12.5;

    vec3  velocity = vec3(dx, dy, dz) * timestep * 0.1;
    velocity = mix(velocity, normalize(velocity), normalizeFactor);

    gl_FragColor = vec4(velocity, 1.0);
}
</script>
<script type="glsl" id="four_wing_attractor_shader">
//addapted from https://www.dynamicmath.xyz/strange-attractors/
uniform float normalizeFactor;
uniform float timestep;

//Four wing Attractor parameters
float a = 0.2;
float b = 0.01;
float c = -0.4;

void main() {

    vec2 uv = gl_FragCoord.xy / resolution.xy;

    vec3 position = texture2D(texturePosition, uv).xyz;

    // Previous positions
    float x = position.x;
    float y = position.y;
    float z = position.z;

    float dx = a*x + y*z;
    float dy = b*x + c*y - x*z;
    float dz = -z - x*y;

    vec3  velocity = vec3(dx, dy, dz) * timestep * 0.1;
    velocity = mix(velocity, normalize(velocity), normalizeFactor);

    gl_FragColor = vec4(velocity, 1.0);
}
</script>
<script type="glsl" id="layer_attractor_shader">
//addapted from http://www.3d-meier.de/tut5a/Seite46.html
uniform float normalizeFactor;
uniform float timestep;

// Layer Attractor parameters
float a_1 = -4.1;
float a_2 = 1.2;
float a_3 = 13.45;
float c_1 = 2.76;
float c_2 = 0.6;
float c_3 = 13.13;
float d = 1.8;

void main() {

    vec2 uv = gl_FragCoord.xy / resolution.xy;

    vec3 pos = texture2D(texturePosition, uv).xyz;

    // Previous positions
    float x = pos.x;
    float y = pos.y;
    float z = pos.z;

    float b = ((d * (a_2 * c_3)*(a_2*c_3))/(32.0*a_3*a_3*c_2*c_2))*sqrt(-(a_3*c_2)/(a_1*c_1));
    float c = (1.0/a_2)*((a_2*a_2*c_3*c_3)/(4.0*a_3*c_2) - (b/d)*(a_3*c_1 + a_1*c_2));
    float dx = a_1*x - a_2*y + a_3*z;
    float dy = -d*x*z + b;
    float dz = c_1*x*y + c_2*y*z + c_3*z + c;

    vec3  velocity = vec3(dx, dy, dz) * timestep * 0.1;
    velocity = mix(velocity, normalize(velocity), normalizeFactor);

    gl_FragColor = vec4(velocity, 1.0);
}
</script>
<script type="glsl" id="position_shader">
    uniform float timer;
    uniform bool includeVelocity;
    uniform float perlinTick;
vec3 mod289(vec3 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x)
{
  return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

vec3 fade(vec3 t) {
  return t*t*t*(t*(t*6.0-15.0)+10.0);
}

// Classic Perlin noise
float cnoise(vec3 P)
{
  vec3 Pi0 = floor(P); // Integer part for indexing
  vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;

  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);

  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);

  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
  return 2.2 * n_xyz;
}
    void main()
    {
        vec2 uv = gl_FragCoord.xy/resolution.xy;
        vec3 tmpPos = texture2D( texturePosition, uv).xyz;
        vec3 velocity = texture2D(textureVelocity, uv).xyz;
        float tmpLife = texture2D(texturePosition, uv).w;
        vec3 pos = tmpPos;

        float life = tmpLife + 1.0;

        if(includeVelocity){
          pos = mix(pos, pos+velocity, 0.5);
        }

        pos += cnoise(vec3(pos.x + perlinTick,pos.y + 1.352*perlinTick,pos.z+12.352*perlinTick)) * 0.5;

        // Write new position out
        gl_FragColor = vec4(pos, 0.0);
        //gl_FragColor = vec4(1,0,0,1);
    }
</script>
<script type="glsl" id="simplex_noise_shader">
    // simulation
    uniform float timer;
    uniform bool includeVelocity;

    vec4 mod289(vec4 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0; }

    float mod289(float x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0; }

    vec4 permute(vec4 x) {
    return mod289(((x*34.0)+1.0)*x);
    }

    float permute(float x) {
    return mod289(((x*34.0)+1.0)*x);
    }

    vec4 taylorInvSqrt(vec4 r)
    {
    return 1.79284291400159 - 0.85373472095314 * r;
    }

    float taylorInvSqrt(float r)
    {
    return 1.79284291400159 - 0.85373472095314 * r;
    }

    vec4 grad4(float j, vec4 ip)
    {
    const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
    vec4 p,s;

    p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
    p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
    s = vec4(lessThan(p, vec4(0.0)));
    p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www;

    return p;
    }

    // (sqrt(5) - 1)/4 = F4, used once below
    #define F4 0.309016994374947451

    float snoise(vec4 v)
    {
    const vec4  C = vec4( 0.138196601125011,  // (5 - sqrt(5))/20  G4
                    0.276393202250021,  // 2 * G4
                    0.414589803375032,  // 3 * G4
                   -0.447213595499958); // -1 + 4 * G4

    // First corner
    vec4 i  = floor(v + dot(v, vec4(F4)) );
    vec4 x0 = v -   i + dot(i, C.xxxx);

    // Other corners

    // Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
    vec4 i0;
    vec3 isX = step( x0.yzw, x0.xxx );
    vec3 isYZ = step( x0.zww, x0.yyz );
    //  i0.x = dot( isX, vec3( 1.0 ) );
    i0.x = isX.x + isX.y + isX.z;
    i0.yzw = 1.0 - isX;
    //  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
    i0.y += isYZ.x + isYZ.y;
    i0.zw += 1.0 - isYZ.xy;
    i0.z += isYZ.z;
    i0.w += 1.0 - isYZ.z;

    // i0 now contains the unique values 0,1,2,3 in each channel
    vec4 i3 = clamp( i0, 0.0, 1.0 );
    vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
    vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

    //  x0 = x0 - 0.0 + 0.0 * C.xxxx
    //  x1 = x0 - i1  + 1.0 * C.xxxx
    //  x2 = x0 - i2  + 2.0 * C.xxxx
    //  x3 = x0 - i3  + 3.0 * C.xxxx
    //  x4 = x0 - 1.0 + 4.0 * C.xxxx
    vec4 x1 = x0 - i1 + C.xxxx;
    vec4 x2 = x0 - i2 + C.yyyy;
    vec4 x3 = x0 - i3 + C.zzzz;
    vec4 x4 = x0 + C.wwww;

    // Permutations
    i = mod289(i);
    float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
    vec4 j1 = permute( permute( permute( permute (
         i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
       + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
       + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
       + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));

    // Gradients: 7x7x6 points over a cube, mapped onto a 4-cross polytope
    // 7*7*6 = 294, which is close to the ring size 17*17 = 289.
    vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

    vec4 p0 = grad4(j0,   ip);
    vec4 p1 = grad4(j1.x, ip);
    vec4 p2 = grad4(j1.y, ip);
    vec4 p3 = grad4(j1.z, ip);
    vec4 p4 = grad4(j1.w, ip);

    // Normalise gradients
    vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
    p0 *= norm.x;
    p1 *= norm.y;
    p2 *= norm.z;
    p3 *= norm.w;
    p4 *= taylorInvSqrt(dot(p4,p4));

    // Mix contributions from the five corners
    vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
    vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
    m0 = m0 * m0;
    m1 = m1 * m1;
    return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
           + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

    }

    void main()
    {
        vec2 uv = gl_FragCoord.xy/resolution.xy;
        vec3 tmpPos = texture2D( texturePosition, uv).xyz;
        vec3 velocity = texture2D(textureVelocity, uv).xyz;
        vec3 pos = tmpPos;

        if(includeVelocity){
          pos = mix(pos, pos+velocity, 0.5);
        }

        pos.x += snoise(vec4(pos.x, pos.y, pos.z, timer/10.0)) * 0.1;
        pos.y += snoise(vec4(pos.x, pos.y, pos.z, 1.352+timer/10.0)) * 0.1;
        pos.z += snoise(vec4(pos.x, pos.y, pos.z, 12.352+timer/10.0)) * 0.1;



        // Write new position out
        gl_FragColor = vec4(pos, 0.0);
        //gl_FragColor = vec4(1,0,0,1);

    }
 </script>
<script type="glsl" id="curl_noise_shader">
    // simulation
    uniform float timer;
    uniform bool includeVelocity;
    uniform float lifeTime;
    uniform float perlinTick;

    vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
  return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r){
  return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v) {

  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

  // First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

  // Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
  //   x1 = x0 - i1  + 1.0 * C.xxx;
  //   x2 = x0 - i2  + 2.0 * C.xxx;
  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

  // Permutations
  i = mod289(i);
  vec4 p = permute( permute( permute(
      i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
    + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
    + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

  // Gradients: 7x7 points over a square, mapped onto an octahedron.
  // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

  //Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

  // Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), dot(p2,x2), dot(p3,x3) ) );

}


// via: https://petewerner.blogspot.jp/2015/02/intro-to-curl-noise.html
vec3 curlNoise( vec3 p ){

  const float e = 0.1;

  float  n1 = snoise(vec3(p.x, p.y + e, p.z));
  float  n2 = snoise(vec3(p.x, p.y - e, p.z));
  float  n3 = snoise(vec3(p.x, p.y, p.z + e));
  float  n4 = snoise(vec3(p.x, p.y, p.z - e));
  float  n5 = snoise(vec3(p.x + e, p.y, p.z));
  float  n6 = snoise(vec3(p.x - e, p.y, p.z));

  float x = n2 - n1 - n4 + n3;
  float y = n4 - n3 - n6 + n5;
  float z = n6 - n5 - n2 + n1;


  const float divisor = 1.0 / ( 2.0 * e );
  return normalize( vec3( x , y , z ) * divisor );
}

//ex uniforms
float delta = 0.4;
float speed = 0.5;
float factor = 0.5;
float evolution = 0.5;
float radius = 2.0;


void main() {
  vec2 uv = gl_FragCoord.xy/resolution.xy;
  vec3 tmpPos = texture2D( texturePosition, uv).xyz;
  vec3 velocity = texture2D(textureVelocity, uv).xyz;
  float tmpLife = texture2D(texturePosition, uv).w;
  vec3 pos = tmpPos;

  float life = tmpLife + 1.0;

  if(includeVelocity){
     pos = mix(pos, pos+velocity, 0.5);
     tmpPos = vec3(0.0);
  }

  float speedInc = 1.0;

  vec3 v = factor * speedInc * delta * speed * ( curlNoise( .2 * pos) );

  pos += v;


  gl_FragColor = vec4( pos, 0.0);
  }
 </script>

<div id="info">
    <h2>Vector field over Perlin Noise.</h2>
    <p>This is a particle system based on a Perlin Noise vector field, implemented with FBO's</p>
    <p>Currently there are 1024 * 1024 particles, there will be more. The system is running on a Razer Blade 2016 with an NVIDIA MX960</p>
    <p>Based on <a href="http://www.subimago.com/?action=viewArticle&articleId=45" target=_blank>experiment</a> by <a href="https://twitter.com/kenirdayaz" target=_blank>@kenirdayaz</a>.</p>
    <p>I'm available at <a id="nametag" href="https://github.com/tianBreznik" target=_blank>@tianBreznik</a>. And so is this project <a href="https://github.com/tianBreznik/particleFlow" target=_blank>@particleflowf</a> </p>
    <h3>!!! ðŸ¥º (click and drag to rotate, scroll to zoom in/out) ðŸ¥º !!!</h3>
</div>

</body>
</html>

<!DOCTYPE html>
<html>
